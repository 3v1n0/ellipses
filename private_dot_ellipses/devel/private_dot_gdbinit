set history save on
set history size 500000
set history filename ~/.gdb_history

define printqs5static
  set $d=$arg0.d
  printf "(Qt5 QString)0x%x length=%i: \"",&$arg0,$d->size
  set $i=0
  set $ca=(const ushort*)(((const char*)$d)+$d->offset)
  while $i < $d->size
    set $c=$ca[$i++]
    if $c < 32 || $c > 127
      printf "\\u%04x", $c
    else
      printf "%c" , (char)$c
    end
  end
  printf "\"\n"
end

define printqs5dynamic
  set $d=(QStringData*)$arg0.d
  printf "(Qt5 QString)0x%x length=%i: \"",&$arg0,$d->size
  set $i=0
  while $i < $d->size
    set $c=$d->data()[$i++]
    if $c < 32 || $c > 127
      printf "\\u%04x", $c
    else
      printf "%c" , (char)$c
    end
  end
  printf "\"\n"
end

define gtype
    set $type = g_type_name(((GTypeInstance*)($arg0))->g_class->g_type)
    printf "type = %s\n", $type
end

define hexdump
   dump binary memory /tmp/gdb-dump.bin $arg0 $arg0+$arg1
   shell hexdump -C /tmp/gdb-dump.bin
   end
end
